/ start:

let callcc = (ret, cont) {
  cont((res) { return(ret, res) })
};

let or = (!cont1, cont2) {
  callcc((ret) {
    cont1(() { ! cont1 }, () { ! cont2 })
  });
};
let and = (!cont1, cont2) {
  callcc((ret) {
    cont1(() { ! cont2 }, () { ! (a, b) { b() } })
  });
};

let if = (ret, v, cont) {
  let break = () { return(ret, 0) };
  v(() { cont(break) }, break)
};
let if_else = (ret, v, cont1, cont2) {
  let break = () { return(ret, 0) };
  v(() { cont1(break) }, () { cont2(break) })
};
let if_ret = (ret, v, cont) {
  let break = () { return(ret, 0) };
  v(() {
    cont();
    break()
  },
  break)
};

let if_else_ret = (!v, cont1, cont2) {
  callcc((ret) {
    v(() {
      let res = cont1();
      ! res
    }, () {
      let res = cont2();
      ! res
    })
  });
};

let ne = (!a, b) {
  callcc((ret) {
  compare(a, b, () { ! (x y) { y() } }, () { ! (x y) { x() } })
  });
};
let not = (!x) {
  callcc((ret) {
    x(() { ! (a b) { b() } }, () { ! (a b) { a() } })
  });
};

let eq = (!a, b) {
  callcc((ret) {
  compare(a, b, () { ! (x y) { x() } }, () { ! (x y) { y() } })
  });
};

let make_nil = (!) { ! (cont_nil, cont_leaf, cont_internal) { cont_nil() } };
let make_leaf = (!l, r) { ! (cont_nil, cont_leaf, cont_internal) { cont_leaf(l, r) } };
let make_internal = (!a, b) { ! (cont_nil, cont_leaf, cont_internal) { cont_internal(a, b) } };

let tree_is_leaf = (!tree) {
  callcc((ret) {
    tree(
      () { ! (a, b) { b() } },
      (l, r) { ! (a, b) { a() } },
      (a, b) { ! (a, b) { b() } }
    )
  });
};

let tree_leaf_get_l = (!tree) {
  callcc((ret) {
    tree(
      () { ! 0 },
      (l, r) { ! l },
      (a, b) { ! 0 }
    )
  });
};

let tree_leaf_get_r = (!tree) {
  callcc((ret) {
    tree(
      () { ! 0 },
      (l, r) { ! r },
      (a, b) { ! 0 }
    )
  });
};

let is_nil = (!tree) {
  ! (a, b) {
    tree(
      () { a() },
      (l, r) { b() },
      (a, c) { b() }
    )
  }
};

let str_equal_func = (!a, b, l, r) {
  let res = str_cmp_func(a, b, l, r);
  ! eq(0, res)
};
  
let print_range = (!a b) {
  if(eq(a, b), (break) { ! 0 });
  print_char(deref_byte(a));
  print_range(add(a, 1), b);
};

let print_num = (!v) {
  if(eq(v, 0), (break) { ! 0 });
  print_num(div(v, 10));
  print_char(add(deref_byte("0"), mod(v, 10)));
};

let printf = (!l) {
  let lb = deref_byte(l);
  if (eq(lb, 0), (break) {
    ! 0
  });
  if (eq(lb, 123), (break) {
    ! (!a, b) {
      print_range(a, b);
      printf(add(l, 2))
    }
  })
  print_char(lb);
  ! printf(add(l, 1))
};

let tree_size = (!tree) {
  callcc((ret) {
  tree(
    () { ! 0 },
    (l, r) { ! 1 },
    (a, b) {
      let a = tree_size(a);
      let b = tree_size(b);
      ! add(a, b)
    }
  )
  });
};

let map_tree = (!tree action) {
  callcc((ret) {
  tree(
    () { ! make_nil() },
    (l, r) {
      ! action(l, r)
    },
    (a, b) {
      let a = map_tree(a, action);
      let b = map_tree(b, action);
      ! make_internal(a, b)
    }
  )
  });
};

let first_leaf = (!tree) {
  callcc((ret) { tree(
    () { ! make_nil() },
    (l, r) { ! make_leaf(l, r) },
    (a, b) {
      let a_leaf = first_leaf(a);
      let b_leaf = first_leaf(b);
      a_leaf(
        () { ! b_leaf },
        (l, r) { ! a_leaf },
        (a, b) { ! b_leaf }
      )
    }
  )
  });
};

let map_tree_iota_help = (!n tree action) {
  callcc((ret) {
  tree(
    () { ! n, make_nil() },
    (l, r) { ! add(n, 1), action(n, l, r) },
    (a, b) {
      let n, new_a = map_tree_iota_help(n, a, action);
      let n, new_b = map_tree_iota_help(n, b, action);
      let res = make_internal(new_a, new_b);
      ! n, res
    }
  )
  });
};

let map_tree_iota = (!tree action) {
  let n, t = map_tree_iota_help(0, tree, action);
  ! t
};

let reverse_tree = (!tree) {
  callcc((ret) { tree(
    () { ! make_nil() },
    (l, r) { ! make_leaf(l, r) },
    (a, b) {
      ! make_internal(reverse_tree(b), reverse_tree(a))
    }
  ) });
};

let search_tree = (!tree l r) {
  callcc(
    (ret) {
      tree(
        () { ! (a, b) { b() } },
        (a, b) {
          let res = str_equal_func(l, r, a, b);
          ! res
        },
        (a, b) {
          let search_a = search_tree(a, l, r);
          if(search_a, (break) { ! search_a });
          let search_b = search_tree(b, l, r);
          ! search_b
        }
      )
    }
  )
};

let is_alpha_bool = (!dig) {
  ! eq(is_alpha(dig), 1)
};

let is_digit_bool = (!dig) {
  ! eq(is_digit(dig), 1)
};

let read_whitespace = (! l r) {
  let lb = deref_byte(l);
  if(eq(l, r), (break) { ! l, r });
  if(or(eq(lb, 10), eq(lb, 32)), (break) {
    let l, r = read_whitespace(add(l, 1), r);
    ! l, r
  });
  ! l, r
};

let read_id_helper = (!l, r) {
  if (eq(l, r), (break) { ! l });
  let lb = deref_byte(l);
  if (or(eq(lb, deref_byte("_")), or(is_digit_bool(lb), is_alpha_bool(lb))), (break) {
    ! read_id_helper(add(l, 1), r)
  });
  ! l
};

let read_identifier = (!l, r) {
  let mid = read_id_helper(l, r);
  ! mid, r, l, mid
};

let matches = (!a b) {
  if(eq(deref_byte(a), 0), (break) { ! (a b) { a() } });
  if(ne(deref_byte(a), deref_byte(b)), (break) { ! (a b) { b() } });
  ! matches(add(a, 1), add(b, 1))
};

let matches_strict = (!pattern, l, r) {
  if (eq(deref_byte(pattern), 0), (break) { ! eq(l, r) });
  if (ne(deref_byte(pattern), deref_byte(l)), (break) { ! (a, b) { b() } });
  ! matches_strict(add(pattern, 1), add(l, 1), r)
};
let token_matches = (!pattern, token) {
  let t, l, r = token();
  ! matches_strict(pattern, l, r)
};

let read_const_string = (!tokenizer) {
  let orig_l, r = tokenizer();
  let l r = read_whitespace(orig_l, r);
  let lb = deref_byte(l);
  if(ne(lb, 34), (break) { ! tokenizer, 0 });
  let l_start = add(l, 1);
  let l_end r = read_until(34, l_start, r);
  let l = add(l_end, 1);
  ! make_tokenizer(l, r), (!) { ! l_start, l_end }
};

let Token_special = 0;
let token_id = 1;
let token_string = 3;
/ 0 -> special, 1 -> id, 2 -> number, 3 -> string, 4 -> comment, 5 -> newline, 6 -> eof
/ returns l r type il ir
let token_eof = 6;
let read_token = (!l, r) {
  let l r = read_whitespace(l, r);
  if (eq(l, r), (break) { ! l, l, Token(token_eof, l, l) });
  if(eq(34, deref_byte(l)), (break) {
    let l_start = add(l, 1);
    let l_end r = read_until(34, l_start, r);
    ! add(l_end, 1), r, Token(token_string, l_start, l_end)
  });
  if(matches("!", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches("(", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches(")", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches("{", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches("}", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches("=", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches(";", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches(":", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches(",", l), (break) { ! add(l, 1), r, Token(0, l, add(l, 1)) });
  if(matches("let", l), (break) { ! add(l, 3), r, Token(0, l, add(l, 3)) });
  if(matches("/", l), (break) { 
    let m, r = read_until(deref_byte("\n"), l, r);
    let l, r, token = read_token(m, r);
    ! l, r, token
  });
  let l r il ir = read_identifier(l, r);
  if(ne(il, ir), (break) { ! l, r, Token(token_id, il, ir) });
  error(l)
};

let Token = (!t, l, r) {
  ! (!) { ! t, l, r }
};
let Token_type = (!token) {
  let t, l, r = token();
  ! t
};

let tokenize = (!s) {
  let l, rr, token = read_token(String_l(s), String_r(s));
  if (eq(Token_type(token), token_eof), (break) {
    ! 0
  });
  ! cons(token, tokenize(String(l, rr)))
};

let read_token_t = (! tokenizer) {
  let l, r = tokenizer();
  let l, r, token = read_token(l, r);
  ! make_tokenizer(l, r), token
};


let make_tokenizer = (!l, r) {
  ! (!) { ! l, r }
};

let token_type = (!token) {
  let t, tl, tr = token();
  ! t
};

let parse_is_returning = (!orig_tokenizer) {
  let tokenizer, token = read_token_t(orig_tokenizer);
  if(token_matches("!", token), (break) {
    ! tokenizer, (a, b) { a() }
  });
  ! orig_tokenizer, (a, b) { b() }
};

let parse_id_list = (!old_tokenizer) {
  let tokenizer, token = read_token_t(old_tokenizer);
  if (eq(token_type(token), 1), (break) {
    let t, tl, tr = token();
    let tokenizer, tree = parse_id_list(tokenizer);
    ! tokenizer, make_internal(make_leaf(tl, tr), tree)
  });
  if (token_matches(",", token), (break) {
    let tokenizer, tree = parse_id_list(tokenizer);
    ! tokenizer, tree
  });
  ! old_tokenizer, make_nil()
};

let print_function = (!old_context, gl, gr, func) {
  let fname_l, fname_r, func_args, is_returning, cexp = func();
  let new_context = merge_context(old_context, func_args);
  let cids = get_cexp_vars(cexp);
  let cids = filter_context(cids, new_context);
  let gl, gr, cids = if_else_ret(is_returning, (!) {
    let gl, gr = gen_label();
    let cids = make_internal(cids, make_leaf(gl, gr));
    ! (!) { ! gl, gr, cids }
  },
  (!) { ! (!) { ! gl, gr, cids } })();
  printf("jmp {}_end\n")(fname_l, fname_r);
  map_tree(make_internal(new_context, cids), (!l r) { printf("{}{}: dq 0\n")(fname_l, fname_r)(l, r); });
  printf("{}:\n")(fname_l, fname_r);
  if_ret(is_returning, (!) {
    printf("pop rax\n");
  });
  map_tree(new_context, (!l r) { printf("pop qword [{}{}]\n")(fname_l, fname_r)(l, r); });
  let new_context = if_else_ret(is_returning, (!) {
    printf("push rax\n");
    printf("mov [{}{}], rsp\n")(fname_l, fname_r)(gl, gr);
    ! make_internal(new_context, make_leaf(gl, gr))
  },
  (!) {
    ! new_context
  });
  print_cexp(fname_l, fname_r, new_context, is_returning, gl, gr, cexp);
  if_ret(is_returning, (!) { printf("ret\n"); });
  printf("{}_end:\n")(fname_l, fname_r);
  ! 0
};

let get_aexp_from_texp = (! texp) {
  if(eq(texp, 0), (break) { ! 0 });
  let texp_type, texp = texp();
  if(eq(texp_type, texp_aexp), (break) {
    ! texp
  });
  ! 0
};

let get_aexp_from_expr = (!expr) {
  if(eq(expr, 0), (break) { ! 0 });
  if(eq(expr_get_type(expr), expr_texp), (break) {
    ! get_aexp_from_texp(expr_texp_get_texp(expr))
  });
  ! 0
};

let texp_string = 0;
let texp_func = 1;
let texp_aexp = 2;
/ returns l r texp
let parse_texp = (!orig_tokenizer) {
  let l r = orig_tokenizer();
  let tokenizer, tk = read_token_t(orig_tokenizer);
  let t, tl, tr = tk();
  if(eq(t, token_id), (break) { ! tokenizer, (!) { ! texp_aexp, (!) { ! tl, tr } } });
  let tokenizer, func = parse_function(orig_tokenizer);
  if(ne(func, 0), (break) {
    ! tokenizer, (!) { ! texp_func, (!) { ! func } }
  });
  let tokenizer, string = read_const_string(tokenizer);
  if(ne(string, 0), (break) {
    ! tokenizer, (!) { ! texp_string, (!) { ! string } }
  });
  ! orig_tokenizer, 0
};

let print_texp = (!fl, fr, old_context, ids, gl, gr, texp) {
  if(eq(texp, 0), (break) { ! 0 });
  let texp_type, texp = texp();
  if (eq(texp_type, texp_string), (break) {
    let string = texp();
    print_const_string(string);
    map_tree(ids, (!l r) { ! printf("pop qword [{}{}]\n")(fl, fr)(l, r) });
    ! 0
  });
  if (eq(texp_type, texp_func), (break) {
    let func = texp();
    let fname_l fname_r func_args is_returning cexp = func();
    let old_context = filter_context(old_context, func_args);
    print_function(old_context, gl, gr, func);
    let context = make_internal(make_leaf(fname_l, fname_r), old_context);
    printf("mov rdi, ");
    let size = tree_size(context);
    print_num(mul(8, add(1, size)));
    printf("\ncall malloc\n");

    map_tree_iota(context, (!n l r) {
      if_else_ret(eq(l, fname_l), (!) {
        printf("mov qword [rax+");
        print_num(mul(8, add(1, n)));
        printf("], {}\n")(l, r);
      }, (!) {
        printf("mov rcx, [{}{}]\nmov qword [rax+")(fl, fr)(l, r);
        print_num(mul(8, add(1, n)));
        printf("], rcx\n");
      });
    });
    printf("mov qword [rax], ");
    print_num(size);
    printf("\npush rax\n");
    map_tree(ids, (!l r) { printf("pop qword [{}{}]\n")(fl, fr)(l, r); });
    ! 0
  });
  if (eq(texp_type, texp_aexp), (break) {
    let a, b = texp();
    print_aexp(old_context, fl, fr, a, b);
    map_tree(ids, (!l r) { ! printf("pop qword [{}{}]\n")(fl, fr)(l, r) });
    ! 0
  });
  ! 0
};

let texp_to_function = (!texp) {
  let texp_type, texp = texp();
  if(eq(texp_type, texp_func), (break) {
    ! texp()
  });
  ! 0
};

let read_until = (!c l r) {
  if (eq(l, r), (break) { ! l, r });
  let lb = deref_byte(l);
  if(eq(lb, c), (break) { ! l, r });
  let l, r = read_until(c, add(l, 1), r);
  ! l, r
};

let print_const_string = (!str) {
  let l_start l_end = str();
  let il ir = gen_label();
  printf("jmp {}_end\n{}: db ")(il, ir)(il, ir);
  print_char(96);
  printf("{}")(l_start, l_end);
  print_char(96);
  printf("\ndb 0\n{}_end: push {}\n")(il, ir)(il, ir);
  ! 0
};

/ lock in
let print_aexp = (!comb_args fname_l fname_r l r) {
  let dig = deref_byte(l);
  let res = is_alpha(dig);
  if(eq(res, 1), (break_o) {
    let bool = search_tree(comb_args, l, r);
    if(bool, (break) {
      printf("push qword [{}{}]\n")(fname_l, fname_r)(l, r);
      ! 0
    });
    break_o()
  });
  if(eq(is_digit(dig), 1), (break) {
    printf("push {}\n")(l, r);
    ! 0
  });
  printf("push qword [{}]\n")(l, r);
  ! 0
};

let filter_context = (!old_context ids) {
  let context = map_tree(old_context, (!l r) {
    if(search_tree(ids, l, r), (break) { ! make_nil() });
    ! make_leaf(l, r)
  });
  ! context
};

let merge_context = (!old_context ids) {
  let context = filter_context(old_context, ids);
  ! make_internal(context, ids)
};

let print_cexp = (!fl fr context is_returning, gl, gr, cexp) {
  let assign cexp = cexp();
  print_statement(fl, fr, context, is_returning, gl, gr, assign);
  if(eq(cexp, 0), (break) { ! 0 });
  let assign_vars = get_assignment_vars(assign);
  print_cexp(fl, fr, merge_context(context, assign_vars), is_returning, gl, gr, cexp);
  ! 0
};

let get_cexp_vars = (!cexp) {
  let assign cexp = cexp();
  let ids = get_assignment_vars(assign);
  if(eq(cexp, 0), (break) { ! ids });
  let ids2 = get_cexp_vars(cexp);
  let ids2 = filter_context(ids2, ids);
  ! make_internal(ids, ids2)
};

let eat_token = (!tokenizer, targ) {
  let new_tokenizer, token = read_token_t(tokenizer);
  if (token_matches(targ, token), (break) {
    ! new_tokenizer, (a, b) { a() }
  })
  ! tokenizer, (a, b) { b() }
};

let get_assignment_vars = (!assign) {
  let assign_type, assign = assign();
  if (eq(assign_type, stmt_assign), (break) {
    let ids, call_exp = assign();
    ! ids
  });
  ! make_nil()
};

let print_call_exp_args = (!fl, fr, old_context, gl, gr, op_list) {
  map_tree(reverse_tree(op_list), (!call_exp r) {
    let il, ir = gen_label();
    printf("jmp {}_end\n{}{}: dq 0\n{}_end:\n")(il, ir)(fl, fr)(il, ir)(il, ir);
    print_expr(fl, fr, old_context, make_leaf(il, ir), gl, gr, call_exp);
    printf("push qword [{}{}]\n")(fl, fr)(il, ir);
  });
  ! 0
};

/ library
let cons = (!head, tail) {
  ! (!) { ! head, tail }
};
let head = (!list) {
  let head, tail = list();
  ! head
};
let tail = (!list) {
  let head, tail = list();
  ! tail
};
let String = (!l, r) {
  ! (!) { ! l, r }
};
let String_l = (!s) {
  let l, r = s();
  ! l
};
let String_r = (!s) {
  let l, r = s();
  ! r
};

let assign_new = (!ids, call_exp) {
  ! (!) { ! ids, call_exp }
};

/ data structs
let stmt_tail_rec = 2;
let stmt_assign = 3;
let stmt_empty_id = 4;
let stmt_return_id = 1;
let stmt_empty = (!) {
  ! (!) { ! stmt_empty_id, 0 }
};
let stmt_type_get = (!stmt) {
  let stmt_type, stmt = stmt();
  ! stmt_type
};
let stmt_return = (!call_exps) {
  ! (!) { ! stmt_return_id, call_exps }
};
let stmt_return_get = (!stmt) {
  let stmt_type, call_exps = stmt();
  ! call_exps
};
let stmt_tail_rec_new = (!expr) {
  ! (!) { ! stmt_tail_rec, expr }
};
let stmt_tail_rec_get = (!stmt) {
  let stmt_type, expr = stmt();
  ! expr
};
let stmt_assign_new = (!assign) {
  ! (!) { ! stmt_assign, assign }
};
let stmt_assign_get = (!stmt) {
  let stmt_type, assign = stmt();
  ! assign
};
let stmt_to_function = (!stmt) {
  if (eq(stmt_type_get(stmt), stmt_assign), (break) {
    let assign = stmt_assign_get(stmt);
    let ids, call_exp = assign();
    let func = expr_to_function(call_exp);
    if(eq(func, 0), (break) { ! 0 });
    ! (!) { ! ids, func }
  });
  ! 0
};

let expr_call = 0;
let expr_texp = 1;
let expr_get_type = (!expr) {
  let expr_type, expr = expr();
  ! expr_type
};
let expr_call_new = (!expr, op_list) {
  ! (!) { ! expr_call, (!) { ! expr, op_list }}
};
let expr_texp_new = (!texp) {
  ! (!) { ! expr_texp, (!) { ! texp }}
};
let expr_texp_get_texp = (!expr) {
  let expr_type, expr = expr();
  let texp = expr();
  ! texp
};
let expr_call_get_expr = (!expr) {
  let expr_type, expr = expr();
  let expr, op_list = expr();
  ! expr
};
let expr_call_get_op_list = (!expr) {
  let expr_type, expr = expr();
  let texp, op_list = expr();
  ! op_list
};
let expr_to_function = (!expr) {
  if (ne(expr_get_type(expr), expr_texp), (break) { ! 0 });
  ! texp_to_function(expr_texp_get_texp(expr))
};



/ parsing
let parse_statement = (!tokenizer) {
  let tokenizer, matches = eat_token(tokenizer, "!");
  if(matches, (break) {
    let tokenizer, call_exps = parse_expr_list(tokenizer);
    ! tokenizer, stmt_return(call_exps)
  });
  let tokenizer, assign = parse_assignment_statement(tokenizer);
  if(ne(assign, 0), (break) {
    ! tokenizer, assign
  });
  let tokenizer, expr = parse_tail_recursion_statement(tokenizer);
  if(ne(expr, 0), (break) {
    ! tokenizer, stmt_tail_rec_new(expr)
  });
  ! tokenizer, stmt_empty()
};

let parse_function = (!orig_tokenizer) {
  let l, r = orig_tokenizer();
  let tokenizer, mt = eat_token(orig_tokenizer, "(");
  if (not(mt), (break) { ! tokenizer, 0 });
  let tokenizer, is_returning = parse_is_returning(tokenizer);
  let fname_l, fname_r = gen_label();
  let tokenizer, new_args = parse_id_list(tokenizer); 
  let tokenizer, mt = eat_token(tokenizer, ")");
  if (not(mt), (break) { ! orig_tokenizer, 0 });
  let tokenizer, block = parse_block(tokenizer);
  if (eq(block, 0), (break) { ! orig_tokenizer, 0 });
  ! tokenizer, (!) { ! fname_l, fname_r, new_args, is_returning, block }
};

let parse_cexp = (!orig_tokenizer) {
  let tokenizer, stmt = parse_statement(orig_tokenizer);
  if(eq(stmt_type_get(stmt), stmt_empty_id), (break) { ! orig_tokenizer, 0 });
  let tokenizer, cexp = parse_cexp(tokenizer);
  ! tokenizer, (!) { ! stmt, cexp }
};

let parse_block = (!tokenizer) {
  let tokenizer, mt = eat_token(tokenizer, "{");
  if (not(mt), (break) { ! tokenizer, 0 });
  let tokenizer, cexp = parse_cexp(tokenizer);
  let tokenizer, mt = eat_token(tokenizer, "}");
  ! tokenizer, cexp
};

let parse_assignment_statement = (!orig_tokenizer) {
  let tokenizer, mt = eat_token(orig_tokenizer, "let");
  let tokenizer, ids, is_assign = (!orig_tokenizer) {
    let tokenizer, ids = parse_id_list(orig_tokenizer);
    let tokenizer, mt = eat_token(tokenizer, "=");
    if(not(mt), (break) {
      let il ir = gen_label();
      ! orig_tokenizer, make_leaf(il, ir), (a, b) { b() }
    });
    ! tokenizer, ids, (a, b) { a() }
  }(tokenizer);
  if (or(and(not(mt), is_assign), and(mt, not(is_assign))), (break) {
    let l, r = tokenizer();
    error(l);
    break()
  });
  let tokenizer, expr = parse_expr(tokenizer);
  let tokenizer, mt = eat_token(tokenizer, ";");
  if(not(mt), (break) { ! orig_tokenizer, 0 });
  ! tokenizer, stmt_assign_new(assign_new(ids, expr))
};

let parse_tail_recursion_statement = (!tokenizer) {
  let l, r = tokenizer();
  let tokenizer, expr = parse_expr(tokenizer);
  if (eq(expr, 0), (break) { ! tokenizer, 0 });
  if (ne(expr_get_type(expr), expr_call), (break) {
    error(l);
    break()
  });
  ! tokenizer, expr
};

/ todo: switch to list
let parse_expr_list = (!tokenizer) {
  let tokenizer, call_exp = parse_expr(tokenizer);
  if(eq(call_exp, 0), (break) { ! tokenizer, make_nil() });
  let l, r = tokenizer();
  let tokenizer, matches = eat_token(tokenizer, ",");
  let tokenizer, tree = parse_expr_list(tokenizer);
  if (and(not(is_nil(tree)), not(matches)), (break) {
    error(l);
    break()
  });
  let leaf = make_leaf(call_exp, 0);
  let tree = make_internal(leaf, tree);
  ! tokenizer, tree
};

let parse_call_args = (!tokenizer) {
  let tokenizer, mt = eat_token(tokenizer, "(");
  if(not(mt), (break) { ! tokenizer, 0 });
  let tokenizer, op_list = parse_expr_list(tokenizer);
  let tokenizer, mt = eat_token(tokenizer, ")");
  if(not(mt), (break2) {
    let l, r = tokenizer();
    error(l);  
  });
  ! tokenizer, op_list
};
let parse_expr = (!tokenizer) {
  let tokenizer, texp = parse_texp(tokenizer);
  if(eq(texp, 0), (break) { ! tokenizer, 0 });
  let tokenizer, expr = parse_expr_help(tokenizer, expr_texp_new(texp));
  ! tokenizer, expr
};
let parse_expr_help = (!tokenizer, expr) {
  let tokenizer, op_list = parse_call_args(tokenizer);
  if(eq(op_list, 0), (break) { ! tokenizer, expr });
  let tokenizer, expr = parse_expr_help(tokenizer, expr_call_new(expr, op_list));
  ! tokenizer, expr
};

let parse_program = (!tokenizer, tokens) {
  let tokenizer, stmt = parse_statement(tokenizer);
  if(eq(stmt_type_get(stmt), stmt_empty_id), (break) { ! 0 });
  let tmp = parse_program(tokenizer, tokens);
  ! cons(stmt, tmp)
};

/ printing
let print_expr = (!fl, fr, old_context, ids, gl, gr, expr) {
  if(eq(expr, 0), (break) { ! 0 });
  if (eq(expr_get_type(expr), expr_texp), (break) {
    print_texp(fl, fr, old_context, ids, gl, gr, expr_texp_get_texp(expr));
    ! 0
  });
  if (eq(expr_get_type(expr), expr_call), (break) {
    let context = map_tree(old_context, (!l r) {
      if(search_tree(ids, l, r), (break) { ! make_nil() });
      ! make_leaf(l, r)
    });
    map_tree(context, (!l r) { ! printf("push qword [{}{}]\n")(fl, fr)(l, r) });
    print_function_application(fl, fr, old_context, (a, b) { a() }, gl, gr, expr);

    let rev_context = reverse_tree(context);
    printf("push rax\n");
    map_tree(ids, (!l r) { ! printf("pop qword [{}{}]\n")(fl, fr)(l, r) });
    map_tree(rev_context, (!l r) { ! printf("pop qword [{}{}]\n")(fl, fr)(l, r) });
    ! 0
  })
  ! 0
};

let print_statement = (!fl, fr, context, is_returning, gl, gr, stmt) {
  if(eq(stmt_type_get(stmt), stmt_return_id), (break) {
    let call_exps = stmt_return_get(stmt);
    printf("mov rsp, [{}{}]\n")(fl, fr)(gl, gr);
    map_tree(reverse_tree(call_exps), (!call_exp r) {
      let il, ir = gen_label();
      printf("jmp {}_end\n{}{}: dq 0\n{}_end:\n")(il, ir)(fl, fr)(il, ir)(il, ir);
      print_expr(fl, fr, context, make_leaf(il, ir), gl, gr, call_exp);
      printf("pop rax\npush qword [{}{}]\npush rax\n")(fl, fr)(il, ir);
    });
    printf("pop rcx\npop rax\njmp rcx\n");
    ! stmt
  });
  if (eq(stmt_type_get(stmt), stmt_assign), (break) {
    let assign = stmt_assign_get(stmt);
    let ids, call_exp = assign();
    print_expr(fl, fr, context, ids, gl, gr, call_exp);
    ! stmt
  });
  if (eq(stmt_type_get(stmt), stmt_tail_rec), (break) {
    print_function_application(fl, fr, context, is_returning, gl, gr, stmt_tail_rec_get(stmt));
    ! stmt
  });
};

let print_function_application = (!fl, fr, context, is_returning, gl, gr, expr) {
  let sub_expr = expr_call_get_expr(expr);
  let op_list = expr_call_get_op_list(expr);
  let al, ar = aexp_to_range(get_aexp_from_expr(sub_expr));
  if_else(or(eq(al, ar), search_tree(context, al, ar)), (break) {
    print_call_exp_args(fl, fr, context, gl, gr, op_list);
    let il, ir = gen_label();
    printf("jmp {}_end\n{}{}: dq 0\n{}_end:\n")(il, ir)(fl, fr)(il, ir)(il, ir);
    print_expr(fl, fr, context, make_leaf(il, ir), gl, gr, sub_expr);
    printf("push qword [{}{}]\n")(fl, fr)(il, ir);
    if_else_ret(is_returning,
      (!) { ! printf("call runtime_call_ret\n") },
      (!) { ! printf("jmp runtime_call\n") },
    );
    break()
  },
  (break) {
    print_call_exp_args(fl, fr, context, gl, gr, op_list);
    if_else_ret(is_returning,
      (!) { ! printf("call {}\n")(al, ar) },
      (!) { ! printf("jmp {}\n")(al, ar) },
    );
    break()
  });
};

let print_program = (!program, context) {
  if (eq(program, 0), (break) { ! 0 });
  let stmt = head(program);

  let id_func = stmt_to_function(stmt);
  if(eq(id_func, 0), (break) {
    let ids = filter_context(get_assignment_vars(stmt), context);
    map_tree(ids, (!l r) { ! printf("jmp {}_end\n{}: dq 0\n{}_end:\n")(l, r)(l, r)(l, r)});
    print_statement(0, 0, make_nil(), (a, b) { a() }, 0, 0, stmt);
    ! print_program(tail(program), make_internal(context, ids))
  });
  if(ne(id_func, 0), (break) {
    let ids func = id_func();
    let fname = first_leaf(ids);
    let fname_l fname_r func_args is_returning cexp = func();
    let func = (!) { ! tree_leaf_get_l(fname), tree_leaf_get_r(fname), func_args, is_returning, cexp };
    print_function(make_nil(), 0, 0, func);
    break()
  });
  ! print_program(tail(program), context)
};

let aexp_to_range = (!aexp) {
  if(eq(aexp, 0), (break) {
    ! 0, 0
  });
  let l r = aexp();
  ! l, r
};

let check_eof = (!tokenizer) {
  let l, r = tokenizer();
  if(eq(l, r), (break) {
    ! (a, b) { a() }
  });
  ! (a, b) { b() }
};

let stderr_print = (!st, len) {
  sysc(1, 2, st, len, 0, 0, 0);
  ! 0
};
let stderr_prints = (!s) {
  if (eq(deref_byte(s), 0), (break) {
    ! 0
  });
  sysc(1, 2, s, 1, 0, 0, 0);
  stderr_prints(add(s, 1));
};
let warning = (!loc) {
  stderr_print("warning:\n", 9);
  stderr_print(sub(loc, 10), 20);
  stderr_print("\n", 1);
  ! 0
};
let error = (!loc) {
  stderr_print("error:\n", 9);
  stderr_print(sub(loc, 10), 20);
  stderr_print("\n", 1);
  exit()
};
let error_message = (!loc, message) {
  stderr_print("error: ", 7);
  stderr_prints(message);
  stderr_print("\n", 1);
  stderr_print(loc, 20);
  stderr_print("\n", 1);
  exit()
};

let inpf_name, outf_name = (!argc, argv1, inpf, outf) { ! inpf, outf }();
let inpf = sysc(2, inpf_name, 0, 65535, 0, 0, 0);
let outf = sysc(2, outf_name, 578, 65535, 0, 0, 0);
/ get input size
let flen = (!) {
  let buff = malloc_func(192);
  sysc(5, inpf, buff, 0, 0, 0, 0);
  ! deref(add(buff, 48))
}();


/ mmap input
let input = sysc(9, 0, flen, 3, 2, inpf, 0);
let flen = add(flen, input);
let buffer = malloc_func(4096);

printf("section .rwx progbits alloc exec write align=16\nglobal _start\n");
printf("add:\npop rbx\npop rax\npop rcx\npush rbx\nadd rax, rcx\nret\n");
printf("sub:\npop rbx\npop rax\npop rcx\npush rbx\nsub rax, rcx\nret\n");
printf("sysc:\npop rcx\npop rax\npop rdi\npop rsi\npop rdx\npop r10\npop r8\npop r9\npush rcx\nsyscall\nret\n");
printf("deref:\npop rdx\npop rax\npush rdx\nmov rax, [rax]\nret\n");
printf("deref_byte:\npop rdx\npop rax\npush rdx\nmov al, [rax]\nand rax, 0xFF\nret\n");
printf("return:\npop rcx\npop rax\npush rcx\nret\n");
printf("mul:\npop rcx\npop rax\npop rdx\nmul rdx\njmp rcx\n");
printf("div:\npop rcx\npop rax\nmov rdx, 0\npop rdi\nidiv rdi\njmp rcx\n");
printf("mod:\npop rcx\npop rax\nmov rdx, 0\npop rdi\nidiv rdi\nmov rax, rdx\njmp rcx\n");
printf("is_digit:\npop rax\npop rdi\npush rax\ncmp rdi, 48\njl not_digit\ncmp rdi, 57\njg not_digit\nyes_digit:\nmov rax, 1\nret\nnot_digit:\nmov rax, 0\nret\n");
printf("is_alpha:\npop rax\npop rdi\npush rax\ncmp rdi, 97\njl nalp\ncmp rdi, 122\njg nalp\nyalp:\nmov rax, 1\nret\nnalp:\ncmp rdi, 65\njl nalp2\ncmp rdi, 90\njg nalp2\nyalp2:\nmov rax, 1\nret\nnalp2:\nmov rax, 0\nret\n");
printf("buff_curr: dq 0\nprint_char:\nmov rax, [buffer]\nmov rdx, [buff_curr]\nmov rcx, [rsp+8]\nmov [rax+rdx], rcx\nadd rdx, 1\nmov [buff_curr], rdx\ncmp rdx, 4096\njne print_char_done\ncall flush_buffer\nprint_char_done:\npop rax\nmov [rsp], rax\nret\nflush_buffer:\nmov rdx, [buff_curr]\nmov rsi, [buffer]\nmov rdi, [outf]\nmov rax, 1\nsyscall\nmov qword [buff_curr], 0\nret\n");
printf("compare: ; takes in (a b equal_cont diff_cont) and calls equal_cont or diff_cont depending on a == b\n  pop rax\n  pop rcx\n  pop rdi\n  pop rsi\n  cmp rax, rcx\n  jne compare_fail\n  push rdi\n  jmp runtime_call\n  compare_fail:\n    push rsi\n    jmp runtime_call\nexit:\n  call flush_buffer\n  mov rax, 60\n  mov rdi, 0\n  syscall\nruntime_call:\n  ; first input is closure containing | number of operands including function, function, environment |\n  mov rdx, [rsp]\n  mov rcx, [rdx]\n  shl rcx, 3\n  add rdx, 8\n  add rsp, 8\n  sub rsp, rcx\n  runtime_call_loop:\n  sub rcx, 8\n  mov rax, [rdx+rcx]\n  mov [rsp+rcx], rax\n  cmp rcx, 0\n  jne runtime_call_loop\n  \n  pop rax\n  jmp rax\nruntime_call_ret:\n  ; first input is closure containing | number of operands including function, function, environment |\n  pop rbp\n  mov rdx, [rsp]\n  mov rcx, [rdx]\n  shl rcx, 3\n  add rdx, 8\n  add rsp, 8\n  sub rsp, rcx\n  runtime_call_ret_loop:\n  sub rcx, 8\n  mov rax, [rdx+rcx]\n  mov [rsp+rcx], rax\n  cmp rcx, 0\n  jne runtime_call_ret_loop\n  \n  pop rax\n  push rbp\n  jmp rax\npage_pointer:\ndq 0\nmemory_left:\ndq 0\nmalloc:\n  push rcx\n  push rdx\n  push rsi\n  push rdi\n  push r8\n  push r9\n  push r10\n  push r11\n  cmp [memory_left], rdi\n  jl new_page\n  jmp malloc_ret\n  \n  new_page:\n  push rdi\n  mov rsi, 4096\n  mov rdi, 0               ; adress\n  mov rdx, 3               ; PROT_READ | PROT_WRITE\n  mov r10, 0x22           ; MAP_SHARED | MAP_ANONYMOUS\n  mov r8, -1              ; file descriptor empty, anonymous\n  mov r9, 0               ; offset\n  mov rax, 9               ; mmap\n  syscall\n  pop rdi\n  mov [page_pointer], rax\n  mov qword [memory_left], 4096\n  malloc_ret:\n  mov rax, [page_pointer]\n  add [page_pointer], rdi\n  sub [memory_left], rdi\n  pop r11\n  pop r10\n  pop r9\n  pop r8\n  pop rdi\n  pop rsi\n  pop rdx\n  pop rcx\n  ret\nmalloc_func:\n  pop rax\n  pop rdi\n  push rax\n  jmp malloc\nscmp:\n  cmp rdi, rsi\n  je end_of_string\n  cmp rdx, rcx\n  je end_of_string\n  sub rax, rax\n  mov al, [rdi]\n  push rbx\n  sub rbx, rbx\n  mov bl, [rdx]\n  sub rax, rbx\n  pop rbx\n  add rdi, 1\n  add rdx, 1\n  cmp rax, 0\n  je scmp\n  jl less\n  mov rax, 1\n  ret\n  less:\n  mov rax, -1\n  ret\n  end_of_string:\n  mov rax, 0\n  cmp rdi, rsi\n  jne not_first_shorter\n  add rax, -1\n  not_first_shorter:\n  cmp rdx, rcx\n  jne not_second_shorter\n  add rax, 1\n  not_second_shorter:\n  ret\ncur_label:\n  dq 1\ngen_label:\n  mov rdi, 8\n  call malloc\n  mov rdx, rax\n  mov rcx, [cur_label]\n  add qword [cur_label], 1\n  mov qword [rdx], 102\n  add rdx, 1\n  gen_label_loop:\n  mov rbx, rcx\n  and rbx, 7\n  add rbx, 48\n  mov [rdx], bl\n  add rdx, 1\n  shr rcx, 3\n  cmp rcx, 0\n  jne gen_label_loop\n  pop rcx\n  push rdx\n  jmp rcx\nstr_cmp_func:\n  pop rax\n  pop rdi\n  pop rsi\n  pop rdx\n  pop rcx\n  push rax\n  call scmp\n  ret\n");
printf("_start:\n");

let tokens = tokenize(String(input, flen));
let tokenizer = make_tokenizer(input, flen);
let res = parse_program(tokenizer, tokens);
print_program(res, make_nil());
exit();
